plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

def libraryVersion = '1.0.3'

android {
    namespace = 'de.kishorrana.signalboy_android'
    compileSdk 33

    publishing {
        // Publishes "release" build variant with "release" component created by
        // Android Gradle plugin
        singleVariant("release") {
            withSourcesJar()
//            withJavadocJar()
        }
    }

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'de.kishorrana'
            artifactId = 'signalboy_android'
            version = libraryVersion

            pom {
                name = "Signalboy helper for Android"
                description = "A library for interfacing with the Signalboy-device."
                url = "https://github.com/kshrana/signalboy-android"
            }

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = 'local'
            url = "${project.buildDir}/repo"
        }
    }
}

dependencies {
    def lifecycle_version = "2.5.1"

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation 'com.tinder.statemachine:statemachine:0.2.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "androidx.lifecycle:lifecycle-runtime-testing:$lifecycle_version"
    testImplementation 'com.natpryce:hamkrest:1.8.0.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

tasks.register('generateRepo', Zip) {
    def publishTask = tasks.named('publishReleasePublicationToLocalRepository')
    from publishTask.map { it.getRepository().getUrl() }
    into 'signalboy_android-maven'
    archiveFileName.set('signalboy_android-maven.zip')
}